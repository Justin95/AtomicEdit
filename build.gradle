apply plugin: 'java'
apply plugin: 'application'

mainClassName = "Main"

def lwjglVersion = "3.2.3" //also dependency of legui
def leguiVersion = "3.2.1"

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "Releases"
        url = "https://raw.github.com/LiquidEngine/repo/releases"
    }
    maven {
        name = "Snapshots"
        url = "https://raw.github.com/LiquidEngine/repo/snapshots"
    }
    maven {
        name = "Develop"
        url = "https://raw.github.com/LiquidEngine/repo/develop"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.lwjgl:lwjgl:$lwjglVersion"
    compile "org.liquidengine:legui:$leguiVersion"
    
    ["natives-windows", "natives-linux", "natives-macos"].each { lwjglNatives ->
        compile "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}:${lwjglNatives}"
    }
    testCompile 'junit:junit:4.12'
}

def getVersion() {
    if (project.hasProperty("tag")) {
        return project.getProperties().get("tag")
    }
    return "Unknown"
}

jar {
    //destinationDirectory = buildDir
    version = getVersion()
    baseName = "AtomicEdit_" + getVersion()
    manifest{
        attributes "Main-Class": "Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it) 
        }
    }
}


processResources {
    def replace = { replaceMap ->
        return { line ->
            def newLine = line
            replaceMap.each { matchRegex, replaceStr ->
                if (line =~ matchRegex) {
                    newLine =  replaceStr
                }
            }
            return newLine
        }
    }
    filesMatching('version.txt', { file ->
        file.filter(replace([
            "atomicedit_version" : getVersion()
        ]))
    })
}
