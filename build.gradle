apply plugin: 'java'
apply plugin: 'application'

mainClassName = "Main"

def atomicEditVersion = "0.1.0"

def lwjglVersion = "3.2.3" //also dependency of legui
def leguiVersion = "3.2.1"

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = "Releases"
        url = "https://raw.github.com/LiquidEngine/repo/releases"
    }
    maven {
        name = "Snapshots"
        url = "https://raw.github.com/LiquidEngine/repo/snapshots"
    }
    maven {
        name = "Develop"
        url = "https://raw.github.com/LiquidEngine/repo/develop"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    //noise library for noise operation
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.lwjgl:lwjgl:$lwjglVersion"
    compile "org.liquidengine:legui:$leguiVersion"
    
    ["natives-windows", "natives-linux", "natives-macos"].each { lwjglNatives ->
        compile "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
        compile "org.lwjgl:lwjgl-tinyfd:${lwjglVersion}:${lwjglNatives}"
    }
    //noise library for noise operation
    implementation 'org.spongepowered:noise:2.0.0-SNAPSHOT'
    testCompile 'junit:junit:4.12'
}

/*
def getVersion() {
    
    def gitTagStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "describe"
        standardOutput = gitTagStdOut
    }
    return gitTagStdOut.toString().split("\n")[0].trim()
}
*/

jar {
    //destinationDirectory = buildDir
    baseName = "AtomicEdit_" + "$atomicEditVersion"
    manifest{
        attributes "Main-Class": "Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it) 
        }
    }
}


processResources {
    def replace = { replaceMap ->
        return { line ->
            def newLine = line
            replaceMap.each { matchRegex, replaceStr ->
                if (line =~ matchRegex) {
                    newLine =  replaceStr
                }
            }
            return newLine
        }
    }
    filesMatching('version.txt', { file ->
        file.filter(replace([
            "atomicedit_version" : atomicEditVersion
        ]))
    })
}
